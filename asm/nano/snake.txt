; last snake direction  = 0x00
; snake length          = 0x01
; food pos              = 0x02
; snake pos             = 0x40 - 0x7f

imh rb  0                   ; set snake's direction to right
iml rb  0
mov ra  rb
iml ra  4
st  ra

iml rb  1                   ; set snake's length to 0
iml ra  0
st  ra

imh rb  4                   ; set snake's position to (3, 3)
iml rb  0
imh ra  3
iml ra  3
st  ra

label gen_food
    in  rd  1               ; input random value from port 1
    imh ra  7               ; mask 0b01110111
    iml ra  7
    and rd
    imh rb  0               ; store to 0x02
    iml rb  2
    st  ra
    mov rb  ra              ; add 0x80
    imh ra  8
    iml ra  0
    add rb
    mov rb  ra          
    imh ra  4               ; draw food 'F'
    iml ra  6
    st  ra
    jnz eat_fin

iml ra  1
jnz loop

label loop
    ; ------ remove snake's tail
    imh rb  4               ; load snake's tail position
    iml rb  0
    ld  rc
    imh ra  8               ; add 0x80
    iml ra  0
    add rc
    mov rb  ra
    imh ra  0               ; set display to space
    iml ra  0
    st  ra
    ; ------

    ; ------ move snake's head
    imh rb  0               ; load snake's length
    iml rb  1
    ld  rd
    imh ra  4               ; add 0x40
    iml ra  0
    add rd
    mov rd  ra              ; keep in rd
    
    imh rb  0               ; load snake's direction
    iml rb  0
    ld  rc
    imh ra  0
    iml ra  1               ; mask 0b00000001
    and rc
    jnz moving_up
    iml ra  2               ; mask 0b00000010
    and rc
    jnz moving_down
    iml ra  4               ; mask 0b00000100
    and rc
    jnz moving_right
    iml ra  8               ; mask 0b00001000
    and rc
    jnz moving_left

    iml ra  1
    jnz moving_fin

    label moving_up
        mov rb  rd
        ld  rc
        imh ra  7
        iml ra  0
        add rc
        mov rc  ra
        imh ra  7
        iml ra  7
        and rc
        st  ra
        jnz moving_fin

    label moving_down
        mov rb  rd
        ld  rc
        imh ra  1
        iml ra  0
        add rc
        mov rc  ra
        imh ra  7
        iml ra  7
        and rc
        st  ra
        jnz moving_fin

    label moving_right
        mov rb  rd
        ld  rc
        imh ra  0
        iml ra  1
        add rc
        mov rc  ra
        imh ra  7
        iml ra  7
        and rc
        st  ra
        jnz moving_fin

    label moving_left
        mov rb  rd
        ld  rc
        imh ra  0
        iml ra  7
        add rc
        mov rc  ra
        imh ra  7
        iml ra  7
        and rc
        st  ra
        jnz moving_fin

    label moving_fin
    ; ------

    ; ------ shift snake's positions
    imh rb  4
    iml rb  0
    label shift_snake
        ld  rd              ; rd <- ram[i]
        imh ra  15          ; i -= 1
        iml ra  15
        add rb
        mov rb  ra
        st  rd              ; ram[i] <- rd
        imh ra  0           ; i += 2
        iml ra  2
        add rb
        mov rb  ra
        imh ra  8
        iml ra  2
        sub ra
        jnz shift_snake
    ; ------

    imh rb  0               ; load snake's length
    iml rb  1
    ld  rc
    imh ra  4               ; add 0x40
    iml ra  0
    add rc
    mov rb  ra              ; load snake's head position
    ld  rc                  ; keep in rc

    imh rb  0               ; load food position
    iml rb  2
    ld  rd                  ; keep in rd
    mov ra  rd
    sub rc                  ; compare
    jnz eat_fin

    imh rb  0               ; load snake's length
    iml rb  1
    ld  ra
    add rb                  ; inc by 1
    st  ra
    imh rb  4
    iml rb  0
    add rb
    st  rd

    iml ra  1
    jnz gen_food

    label eat_fin

    imh ra  8               ; add 0x80
    iml ra  0
    add rc
    mov rb  ra
    imh ra  5               ; draw snake's head 'S'
    iml ra  3
    st  ra

    in  ra  0               ; update snake's direction
    jnz change_movement
    iml ra  1
    jnz change_movement_end
    label change_movement
        imh rb  0
        iml rb  0
        st  ra    
    label change_movement_end

    jnz loop

hlt